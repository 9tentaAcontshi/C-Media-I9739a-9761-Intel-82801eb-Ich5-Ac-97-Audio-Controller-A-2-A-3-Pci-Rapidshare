plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "org.moditect.gradleplugin" version "1.0.0-rc3"
}

apply from: 'publish.gradle'
apply from: "$rootDir/deps/jackson.gradle"
apply from: "$rootDir/deps/slf4j.gradle"
apply from: "$rootDir/deps/lombok.gradle"
apply from: "$rootDir/deps/commons.gradle"
apply from: "$rootDir/deps/javafx.gradle"
apply from: "$rootDir/deps/javafx-static.gradle"

version '0.1'
group 'io.xpipe'
archivesBaseName = 'xpipe'

java {
    modularity.inferModulePath = true
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    compileOnly project(':fxcomps')
}

repositories {
    mavenCentral()
}

javadoc{
    source = sourceSets.main.allJava
    options {
        addStringOption('-release', '17')
        addStringOption('link', 'https://docs.oracle.com/en/java/javase/17/docs/api/')
    }
}

sourceSets {
    main {
        java {
            srcDir(projectDir)

            srcDir("../api/src/main/java")
            exclude {
                return it.getFile() == file("../api/src/main/java/module-info.java")
            }

            srcDir("../core/src/main/java")
            exclude {
                return it.getFile() == file("../core/src/main/java/module-info.java")
            }

            srcDir("../beacon/src/main/java")
            exclude {
                return it.getFile() == file("../beacon/src/main/java/module-info.java")
            }

            srcDir("../extension/src/main/java")
            exclude {
                return it.getFile() == file("../extension/src/main/java/module-info.java")
            }
        }
    }
}
